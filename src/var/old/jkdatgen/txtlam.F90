SUBROUTINE TXTLAM(N, ISEED, LAM, NPOS, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: N
  INTEGER, INTENT(INOUT) :: ISEED(4)
  REAL(KIND=8), INTENT(OUT) :: LAM(N)
  INTEGER, INTENT(OUT) :: NPOS, INFO

  REAL(KIND=8), PARAMETER :: ZERO = +0.0E+0_8

  INTEGER :: I
  LOGICAL :: FEX
  CHARACTER(LEN=256) :: FN

  EXTERNAL :: SEEDOK

  CALL SEEDOK(ISEED, I)

  IF (N .LT. 0) THEN
    INFO = -1
  ELSE IF (I .NE. 0) THEN
    INFO = -2
  ELSE IF (COMMAND_ARGUMENT_COUNT() .NE. 7) THEN
    INFO = -3
  ELSE
    INFO = 0
  END IF
  IF (INFO .NE. 0) RETURN

  CALL GET_COMMAND_ARGUMENT(7, FN)
  IF (LEN_TRIM(FN) .EQ. 0) THEN
    INFO = 1
    RETURN
  END IF

  INQUIRE(FILE=FN, EXIST=FEX)
  IF (.NOT. FEX) THEN
    INFO = 2
    RETURN
  END IF

  OPEN(UNIT=1,FILE=FN,ACTION='READ',STATUS='OLD')

  NPOS = 0
  DO I = 1, N
    READ (1,*) LAM(I)
    IF (LAM(I) .GT. ZERO) NPOS = NPOS + 1
  END DO

  CLOSE(UNIT=1)

END SUBROUTINE TXTLAM
