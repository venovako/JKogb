PROGRAM DJK
  USE OMP_LIB
  USE BINIO
  USE DSTEP
  IMPLICIT NONE

  CHARACTER(LEN=FNL) :: FN
  INTEGER :: N, N_2, ID_NCP, ID_TRU, INFO, NN, NM, NT, TT, FD(3)
  TYPE(DPROC) :: R

  REAL(KIND=DWP), ALLOCATABLE, TARGET :: DARR(:,:)
  REAL(KIND=DWP), POINTER, CONTIGUOUS :: A(:,:), U(:,:), Z(:,:), S(:)
  INTEGER, ALLOCATABLE, TARGET :: IARR(:)
  INTEGER, POINTER, CONTIGUOUS :: J(:), P(:), Q(:), STEP(:)
  TYPE(AW), ALLOCATABLE, TARGET :: DZ(:)

  ! IF (.NOT. VERIFY_MIN_MAX(.FALSE.)) ERROR STOP 'MIN/MAX do not handle the NaNs properly'
#ifndef NDEBUG
  CALL SetCtrlC
#endif
  CALL READCL(FN, N, N_2, ID_NCP, ID_TRU, INFO)
  IF (INFO .NE. 0) ERROR STOP 'djk.exe FN N N_2 [ID_NCP [ID_TRU]]'
#ifndef NDEBUG
  WRITE (ERROR_UNIT,'(A,A)')   '    FN=', TRIM(FN)
  FLUSH(ERROR_UNIT)
  WRITE (ERROR_UNIT,'(A,I11)') '     N=', N
  FLUSH(ERROR_UNIT)
#endif
  IF (N .LE. 1) ERROR STOP 'N < 2'

  INFO = JSTEP_LEN(N, N_2)
  N_2 = INFO
#ifndef NDEBUG
  WRITE (ERROR_UNIT,'(A,I11)') '   N_2=', N_2
  FLUSH(ERROR_UNIT)
#endif
  IF (INFO .LE. 0) ERROR STOP 'JSTEP_LEN'

  ! number of threads
  NT = MIN(MAX(1, INT(OMP_GET_MAX_THREADS())), N_2)
#ifndef NDEBUG
  WRITE (ERROR_UNIT,'(A,I11)') '    NT=', NT
  FLUSH(ERROR_UNIT)
#endif

  CALL DPROC_INIT(NT, ID_NCP, ID_TRU, R, INFO)
#ifndef NDEBUG
  WRITE (ERROR_UNIT,'(A,I11)') 'ID_NCP=', ID_NCP
  FLUSH(ERROR_UNIT)
  WRITE (ERROR_UNIT,'(A,I11)') 'ID_TRU=', ID_TRU
  FLUSH(ERROR_UNIT)
#endif
  IF (INFO .NE. 0) ERROR STOP 'DPROC_INIT'

  CALL OPEN_YJ_RO(FN, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'OPEN_YJ_RO'
  END IF

  ALLOCATE(DARR(N, 3 * N + 1))
  A => DARR(:,1:N)
  U => DARR(:,N+1:2*N)
  Z => DARR(:,2*N+1:3*N)
  S => DARR(:,3*N+1)

  ALLOCATE(IARR(N * N + N_2))
  NN = (N * (N - 1)) / 2
  J => IARR(1:N)
  P => IARR(N+1:NN+N)
  Q => IARR(NN+N+1:2*NN+N)
  STEP => IARR(2*NN+N+1:2*NN+N+N_2)

  INFO = MOD(NN, NT)
  IF (INFO .GT. 0) INFO = NT - INFO
  NM = 2 * (NN + INFO)
  ALLOCATE(DZ(NM))
#ifndef NDEBUG
  WRITE (ERROR_UNIT,'(A,I11)') '    NM=', NM
  FLUSH(ERROR_UNIT)
#endif

  CALL DREAD_YJ(FD, A, J, N, N, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'DREAD_YJ'
  END IF
  CALL BCLOSEN(2, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BCLOSEN(RO)'
  END IF

  CALL R%TRU(N, J, NN, P, Q, FD, INFO)
  IF (INFO .LT. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'R%TRU'
  END IF

  TT = INFO
  WRITE (OUTPUT_UNIT,'(3(A,I11),A)') 'J has', FD(1), ' +  and', FD(2), ' - signs for <=', TT, ' trig. transfs.'
  FLUSH(OUTPUT_UNIT)

  FD(1) = GET_SYS_TIME()
  CALL DSTEP_LOOP(NT, N, U, N, A, N, Z, N, J, S, NN, P, Q, R, NM, DZ, N_2, STEP, TT, INFO)
  FD(1) = GET_SYS_TIME() - FD(1)
  IF (INFO .GE. 0) THEN
     WRITE (OUTPUT_UNIT,'(A,I10,A,F13.6,A)') 'Executed ', INFO, ' steps with transformations in ', &
          (FD(1) / REAL(GET_SYS_TRES(),DWP)), ' s'
     FLUSH(OUTPUT_UNIT)
  ELSE ! error
     WRITE (ERROR_UNIT,'(A,I10,A,F13.6,A)') 'ERROR ', INFO, ' after ', &
          (FD(1) / REAL(GET_SYS_TRES(),DWP)), ' s'
     FLUSH(ERROR_UNIT)
  END IF

  IF (ALLOCATED(DZ)) DEALLOCATE(DZ)
  STEP => NULL()
  Q => NULL()
  P => NULL()
  J => NULL()
  IF (ALLOCATED(IARR)) DEALLOCATE(IARR)

  CALL OPEN_UZS_WO(FN, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'OPEN_UZS_WO'
  END IF

  CALL DWRITE_UZS(FD, U, Z, S, N, N, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'DWRITE_UZS'
  END IF

  CALL BCLOSEN(3, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BCLOSEN(WO)'
  END IF

  S => NULL()
  Z => NULL()
  U => NULL()
  A => NULL()
  IF (ALLOCATED(DARR)) DEALLOCATE(DARR)

CONTAINS
#include "readcl.F90"
#include "bio.F90"
END PROGRAM DJK
