PROGRAM zjk
  !$ USE omp_lib
  USE binio
#ifdef USE_FAST
  USE timer
#endif
  USE zstep
  IMPLICIT NONE

  CHARACTER(LEN=FNL) :: FN
  INTEGER :: N, N_2, NN, NM, NT, TT, INFO
  !$ SAVE :: N, N_2, NN, NM, NT, TT, INFO
  INTEGER :: ID_NCP, ID_TRU, FD(3)
  TYPE(ZPROC) :: R

  COMPLEX(KIND=DWP), ALLOCATABLE :: A(:,:), U(:,:), Z(:,:)
  REAL(KIND=DWP), ALLOCATABLE :: S(:)
  INTEGER, ALLOCATABLE :: J(:), P(:), Q(:), STEP(:)
  TYPE(AW), ALLOCATABLE :: DZ(:)
  COMPLEX(KIND=DWP), ALLOCATABLE :: W(:,:,:)

  IF (.NOT. VERIFY_MIN_MAX(.FALSE.)) STOP 'MIN/MAX do not handle the NaNs properly'
  CALL READCL(FN, N, N_2, ID_NCP, ID_TRU, INFO)
  IF (INFO .NE. 0) STOP 'zjk.exe FN N N_2 [ID_NCP [ID_TRU]]'
  IF (N .LE. 2) STOP 'N should be at least 3 (for N=2 use ztest)'

  INFO = JSTEP_LEN(N, N_2)
  N_2 = INFO
  IF (INFO .LE. 0) STOP 'JSTEP_LEN'
  NN = (N * (N - 1)) / 2
  ! number of threads
  NT = 1
  !$ NT = MAX(NT, INT(OMP_GET_MAX_THREADS()))
  INFO = MOD(NN, NT)
  IF (INFO .GT. 0) INFO = NT - INFO
  NM = 2 * (NN + INFO)
  CALL ZPROC_INIT(NT, ID_NCP, ID_TRU, R, INFO)
  IF (INFO .NE. 0) STOP 'ZPROC_INIT'

  CALL OPEN_YJ_RO(FN, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'OPEN_YJ_RO'
  END IF

  ALLOCATE(A(N,N))
  ALLOCATE(U(N,N))
  ALLOCATE(Z(N,N))
  ALLOCATE(S(N))
  ALLOCATE(J(N))
  ALLOCATE(P(NN))
  ALLOCATE(Q(NN))
  ALLOCATE(STEP(N_2))
  ALLOCATE(DZ(NM))
  ALLOCATE(W(2,3,N_2))

  CALL ZREAD_YJ(FD, A, J, N, N, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'ZREAD_YJ'
  END IF
  CALL BCLOSEN(2, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'BCLOSEN(RO)'
  END IF

#ifdef USE_SUN
  SELECT CASE (R%TRU)
  CASE (1)
     CALL TRU1(N, J, NN, P, Q, FD, INFO)
  CASE (2)
     CALL TRU2(N, J, NN, P, Q, FD, INFO)
  CASE DEFAULT
     STOP 'R%TRU'
  END SELECT
#else
  CALL R%TRU(N, J, NN, P, Q, FD, INFO)
#endif
  IF (INFO .LT. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'R%TRU'
  END IF

  TT = INFO
  WRITE (OUTPUT_UNIT,'(3(A,I11),A)') 'J has', FD(1), ' +  and', FD(2), ' - signs for <=', TT, ' trig. transfs.'
  FLUSH(OUTPUT_UNIT)

  FD(1) = GET_SYS_TIME()
  CALL ZSTEP_LOOP(N, U, N, A, N, Z, N, J, S, NN, P, Q, R, NM, DZ, N_2, STEP, TT, W, INFO)
  FD(1) = MAX(GET_SYS_TIME() - FD(1), 0)
  IF (INFO .GE. 0) THEN
     WRITE (OUTPUT_UNIT,'(A,I11,A,F13.6,A)') 'Executed ', INFO, ' steps with transformations in ', &
          (FD(1) / REAL(GET_SYS_TRES(),DWP)), ' s'
     FLUSH(OUTPUT_UNIT)
  ELSE ! error
     WRITE (ERROR_UNIT,'(A,I11,A,F13.6,A)') 'ERROR ', INFO, ' after ', &
          (FD(1) / REAL(GET_SYS_TRES(),DWP)), ' s'
     FLUSH(ERROR_UNIT)
  END IF

  IF (ALLOCATED(W)) DEALLOCATE(W)
  IF (ALLOCATED(DZ)) DEALLOCATE(DZ)
  IF (ALLOCATED(STEP)) DEALLOCATE(STEP)
  IF (ALLOCATED(Q)) DEALLOCATE(Q)
  IF (ALLOCATED(P)) DEALLOCATE(P)
  IF (ALLOCATED(J)) DEALLOCATE(J)

  CALL OPEN_UZS_WO(FN, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'OPEN_UZS_WO'
  END IF

  CALL ZWRITE_UZS(FD, U, Z, S, N, N, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'ZWRITE_UZS'
  END IF

  CALL BCLOSEN(3, FD, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(A,I11)') 'INFO=', INFO
     FLUSH(ERROR_UNIT)
     STOP 'BCLOSEN(WO)'
  END IF

  IF (ALLOCATED(S)) DEALLOCATE(S)
  IF (ALLOCATED(Z)) DEALLOCATE(Z)
  IF (ALLOCATED(U)) DEALLOCATE(U)
  IF (ALLOCATED(A)) DEALLOCATE(A)

CONTAINS
#include "readcl.F90"
#include "bio.F90"
END PROGRAM zjk
